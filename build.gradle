buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.netflix.nebula:nebula-project-plugin:9.6.3'
    }
}
description='A gradle plugin that generates jboss-deployment-structure.xml based on the deploy configuration in the ear'
group = 'com.github.mamohr.gradle'

apply plugin: 'nebula.contacts-base'
apply plugin: 'nebula.integtest'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    testImplementation 'junit:junit:4.12'
    testImplementation 'xmlunit:xmlunit:1.6'

    testImplementation 'com.netflix.nebula:nebula-test:10.2.0'
}

ext {
    if (!project.hasProperty("bintrayUser")) {
        bintrayUser = ""
    }
    if (!project.hasProperty("bintrayKey")) {
        bintrayKey = ""
    }
}

configurations.all {
    resolutionStrategy {
        exclude group: 'org.codehaus.groovy', module: 'groovy'
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
}


task sourcesJar(type:Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

task testsJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.test.output + sourceSets.test.allSource
}

publishing {
    repositories {
        maven {
            url localMavenRepository
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact testsJar
        }
    }
}
